plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.mavenPublish)
}

android {
    compileSdk = rootProject.ext.compileSdk
    namespace = "dev.drewhamilton.androidtime.threetenbp.format"
    testNamespace = "dev.drewhamilton.androidtime.threetenbp.format.test"

    defaultConfig {
        minSdkVersion rootProject.ext.threeTenAbpMinSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias keyAlias
            keyPassword keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    api(libs.threeTen.abp)
    // Can't use ":no-tzdb" in libs.versions.toml:
    api("org.threeten:threetenbp:1.6.8:no-tzdb")

    implementation(libs.androidx.annotation)

    androidTestImplementation(libs.kotlin.stdlib)
    androidTestImplementation(project(":test"))
}
