import com.vanniktech.maven.publish.JavadocJar
import com.vanniktech.maven.publish.MavenPublishBaseExtension

plugins {
    alias(libs.plugins.android.app) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.mavenPublish) apply false

    alias(libs.plugins.kotlinx.binaryCompatibilityValidator)
}

apply plugin: 'binary-compatibility-validator'
apiValidation {
    ignoredProjects += ['demo']
}

allprojects { project ->
    group = rootProject.property("GROUP")
    version = rootProject.property("VERSION_NAME")

    repositories {
        mavenCentral()
        google()
    }
}

subprojects {
    plugins.withId("com.vanniktech.maven.publish") {
        extensions.getByType(MavenPublishBaseExtension.class)
            .configureBasedOnAppliedPlugins(new JavadocJar.None())
    }
}

ext {
    compileSdk = 36
    minSdk = 26

    keystore = project.hasProperty('personalKeystore') ? personalKeystore : 'x'
    keystorePassword = project.hasProperty('personalKeystorePassword') ? personalKeystorePassword : 'x'
    keyAlias = project.hasProperty('personalKeyAlias') ? personalKeyAlias : 'x'
    keyPassword = project.hasProperty('personalKeyPassword') ? personalKeyPassword : 'x'
}
