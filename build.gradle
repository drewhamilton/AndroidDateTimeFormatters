buildscript {
    ext {
        versions = [
            kotlin: '1.3.72'
        ]
        deps = [
            androidTestRules: 'androidx.test:rules:1.2.0',
            androidXAnnotations: 'androidx.annotation:annotation:1.1.0',
            desugarJdkLibs: 'com.android.tools:desugar_jdk_libs:1.0.5',
            junit: 'junit:junit:4.13',
            junitAndroid: 'androidx.test.ext:junit:1.1.1',
            kotlinStdLib: "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin",
            multidex: 'com.android.support:multidex:1.0.3',
            threeTenAbp: 'com.jakewharton.threetenabp:threetenabp:1.2.4',
            threeTenBp: 'org.threeten:threetenbp:1.4.4:no-tzdb',
            truth: 'com.google.truth:truth:1.0.1'
        ]
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"

        classpath 'org.jetbrains.kotlinx:binary-compatibility-validator:0.2.3'

        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }

    repositories {
        google()
        gradlePluginPortal()
        mavenCentral()
    }
}

apply plugin: 'binary-compatibility-validator'
apiValidation {
    ignoredProjects += ['test', 'app']
}

allprojects { project ->
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    project.plugins.withType(com.android.build.gradle.LibraryPlugin) {
        project.android.libraryVariants.all { variant ->
            // TODO WORKAROUND: replace with https://issuetracker.google.com/issues/72050365 once released.
            variant.generateBuildConfigProvider.get().enabled = false
        }
    }
}

ext {
    libraryVersion = '2.2.0-SNAPSHOT'

    buildToolsVersion = '29.0.3'
    compileSdk = 29
    multidexMinSdk = 4
    threeTenAbpMinSdk = 15
    androidXMinSdk = 14

    keystore = project.hasProperty('personalKeystore') ? personalKeystore : 'x'
    keystorePassword = project.hasProperty('personalKeystorePassword') ? personalKeystorePassword : 'x'
    keyAlias = project.hasProperty('personalKeyAlias') ? personalKeyAlias : 'x'
    keyPassword = project.hasProperty('personalKeyPassword') ? personalKeyPassword : 'x'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
